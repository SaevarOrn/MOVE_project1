* P is a template for a cell
proc P = 'in.a.out.b.P

* Template instantiations 
proc P1 = P[c3/in, a1/a, c1/out, b1/b]
proc P2 = P[c1/in, a2/a, c2/out, b2/b]
proc P3 = P[c2/in, a3/a, c3/out, b3/b]

* S is the scheduler
proc S = (P1|P2|P3|c3.nil)\{c1, c2, c3}

* This process is used to verify that the a's are excecuted in a cycle
proc Cycle = a1.a2.a3.Cycle

* This process is used to verify that each cell alternates between a and b
proc Alt = a.b.Alt

* Used to hide in and out actions of the processes being scheduled
proc Z = in.'out.Z

* Used to hide the b actions of the processes being scheduled
proc B = 'b1.B+'b2.B+'b3.B

* command 'eq Y Cycle' confirms that the scheduler is cyclical
proc Y = (S|B)\{b1,b2,b3}

* command 'eq W Alt' confirms that the schedular is alternating on a and b
proc W = (P|Z)\{in,out}

